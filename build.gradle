buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1.6.1"
group = "me.glorantq"
archivesBaseName = "aboe-discord"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "eclipse"
}

ext {
    lombok_version = "1.16.20"
    discord_version = "v1.3.6"
    json_version = "1.1.1"
    lang3_version = "3.7"
    st4_version = "4.0.8"
}

repositories {
    maven { url 'https://jitpack.io' }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed "org.projectlombok:lombok:$lombok_version"
    embed "com.github.MinnDevelopment:java-discord-rpc:$discord_version"
    embed "com.googlecode.json-simple:json-simple:$json_version"
    embed "org.antlr:ST4:$st4_version"
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
